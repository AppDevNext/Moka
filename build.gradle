import org.gradle.internal.jvm.Jvm

buildscript {

    ext.kotlin_version = '1.6.21'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.13'
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }

}

ext {
    revision = getGitCommitHash()
    versionRevision = getGitCommitCount()

    sdkVersion = 31
    androidxTestEspresso = '3.4.0'
}

println "Gradle uses Java ${Jvm.current()}"

allprojects {

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

}

private static def getGitCommitCount() {
    try {
        return 'git rev-list --first-parent --count HEAD'.execute().text.trim() as Integer
    }
    catch (ignored) {
        return 0
    }
}

private static def getGitCommitHash() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    }
    catch (ignored) {
        return null
    }
}