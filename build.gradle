import org.gradle.internal.jvm.Jvm

buildscript {
    ext.kotlin_version = '1.8.10'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.android.tools.build:gradle:7.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    revision = getGitCommitHash()
    versionRevision = getGitCommitCount()

    sdkVersion = 33
    androidxTestEspresso = '3.5.1'
    androidxTestOrchestrator = '1.1.1'
    androidxTestExtJunit = '1.1.0'
    androidTools = '30.3.1' // For Android 4.0, it's 27.0.0-beta01 https://mvnrepository.com/artifact/com.android.tools.lint/lint-api?repo=google
}

println "Gradle uses Java ${Jvm.current()}"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

private static def getGitCommitCount() {
    try {
        return 'git rev-list --first-parent --count HEAD'.execute().text.trim() as Integer
    }
    catch (ignored) {
        return 0
    }
}

private static def getGitCommitHash() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    }
    catch (ignored) {
        return null
    }
}
