plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace 'com.sample.app'

    defaultConfig {
        minSdkVersion 21
        compileSdk sdkVersion
        targetSdkVersion sdkVersion

        applicationId "com.sample.app"
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: 'true'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding = true
        buildConfig true
    }

    testOptions {
        managedDevices {
            devices {
                // run with ../gradlew nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // Also use the AOSP ATD image for better emulator performance
                    // The androidx.test screenshot APIs will automatically enable hardware rendering
                    // to take a screenshot
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestUtil "androidx.test.services:test-services:1.4.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:${androidxTestEspresso}"

    androidTestImplementation project(':moka')
}
